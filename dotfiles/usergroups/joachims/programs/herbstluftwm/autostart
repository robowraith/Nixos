#! /usr/bin/env fish

function hc
    herbstclient $argv
end

# Configuration for Herbtluftwm
hc emit_hook reload
# Setting the Modifier key
set Mod Mod4

# Other config
source ~/.config/herbstluftwm/conf.d/keybindings.fish
source ~/.config/herbstluftwm/conf.d/rules.fish
source ~/.config/herbstluftwm/conf.d/theme.fish
source ~/.config/herbstluftwm/conf.d/tags.fish
source ~/.config/herbstluftwm/conf.d/screen_setup.fish

hc unlock

# Autostart applications
# check if this is the first run
if hc silent new_attr bool my_first_run

    # Implement Notebook-Screen fix
    sudo /home/joachim/psr_fix/psr_fix.sh

    # Start polkit agent
    hc spawn lxqt-policykit-agent

    # Start applications that do not depend on secret-service or ssh-agent

    # Start composition manager
    hc spawn picom
    # Hide the mouse pointer after a while
    hc spawn unclutter -b
    # Set numlock to on
    hc spawn numlockx on
    # Start Notification agent
    hc spawn dunst
    # Start Bluetoohth manager applet
    hc spawn blueman-applet
    # Start clipboard tool
    hc spawn copyq
    # Start Screenshot tool
    hc spawn flameshot

    # Start Keepassxc and wait for it to be unlocked
    hc focus_monitor 3
    hc and , \
        rule once class="KeePassXC" tag="left_main" , \
        spawn keepassxc

    while ! secret-tool lookup KeepassXC 'unlocked?'
    end

    # start SSH-Agent
    if not pgrep -f ssh-agent >/dev/null
        eval (ssh-agent -c)
        set -Ux SSH_AUTH_SOCK $SSH_AUTH_SOCK
        set -Ux SSH_AGENT_PID $SSH_AGENT_PID
        set -Ux SSH_AUTH_SOCK $SSH_AUTH_SOCK
    end

    # Start applications that depend on secret-service or SSH-Agent
    # Start Control Panel applications
    hc spawn nm-applet
    hc spawn nextcloud --background

    # Set up private tag
    hc and , \
        rule once class="Signal" tag="left_main" , \
        spawn signal-desktop

    # Start main applications
    hc and , \
        rule once class="Vivaldi-stable" tag="main_main", \
        spawn vivaldi , \
        rule once title="general_terminal" tag="right_main", \
        spawn kitty

    # Set up main screens
    hc chain , \
        focus_monitor left , \
        use left_main , \
        focus_monitor right , \
        use right_main , \
        focus_monitor main , \
        use main_main
end
